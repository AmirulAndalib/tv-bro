apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.phlox.tvwebbrowser"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 32
        versionName "1.4.5"
    }
    signingConfigs {
        release {
            storeFile rootProject.file(properties.getProperty('storeFile'))
            storePassword properties.getProperty('storePassword')
            keyAlias properties.getProperty('keyAlias')
            keyPassword properties.getProperty('keyPassword')
        }
    }
    buildTypes {
        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "crashlytics", "abi"
    productFlavors {
        crashlyticsfree {
            dimension "crashlytics"
        }
        crashlytics {
            dimension "crashlytics"
            //versionNameSuffix "-crashlytics"
        }

        x86     { dimension "abi" }
        x86_64  { dimension "abi" }
        arm     { dimension "abi" }
        aarch64 { dimension "abi" }
    }

    lintOptions {
        abortOnError false
    }

    // Required for GeckoARM configuration
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    support_lib_version = '28.0.0'
    lifecycle_version = "1.1.1"
    geckoviewChannel = "nightly"
    geckoviewVersion = "69.0.20190618041042"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:support-v4:$support_lib_version"
    implementation "com.android.support:appcompat-v7:$support_lib_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version"

    implementation 'com.github.truefedex:ASQL:v0.1.2'
    implementation 'com.github.truefedex:segmented-button:v1.0.0'
    implementation 'de.halfbit:pinned-section-listview:1.0.0'

    crashlyticsImplementation 'com.google.firebase:firebase-core:16.0.9'
    crashlyticsImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    x86Implementation     "org.mozilla.geckoview:geckoview-${geckoviewChannel}-x86:${geckoviewVersion}"
    x86_64Implementation  "org.mozilla.geckoview:geckoview-${geckoviewChannel}-x86_64:${geckoviewVersion}"
    armImplementation     "org.mozilla.geckoview:geckoview-${geckoviewChannel}-armeabi-v7a:${geckoviewVersion}"
    aarch64Implementation "org.mozilla.geckoview:geckoview-${geckoviewChannel}-arm64-v8a:${geckoviewVersion}"
}
